; FINAN-Import-Basis.fsf
; versie 10-05-2010 (EJS)
;
{&&Language=Nederlands}

;=============================================================================================================
; Import menu
; zie voor alle ImportOrigin IDs  en bijbehorende bestandsnamen: http://intranet.findesk.com/index.php/SIL_ID

.Application HideSilImportMenuItems=On      ; LET OP: kan ook in ini-file (in dat geval hier weghalen!!)

;—————————————————————————————————————————————————————————————————————————————————————————————————————————————
; Unit4 Audition (classic)

.If FileExists("#ApplicationFolder#\AUDITION_XML_SIL.dll")
  .Script Name=AuditionClassicImportScript,Title="Unit4 Audition (classic)"
  .Event OnFileImportMenuClick
    .Let ImportOrigin="AUDITION_XML"
    .DoScript GeneralImportScript
  .EndScript
.EndIf

;—————————————————————————————————————————————————————————————————————————————————————————————————————————————
; Unit4 Audition (Rating export)

.If FileExists("#ApplicationFolder#\FinanXML_SIL.dll")
  .Script Name=AuditionFinanImportScript,Title="Unit4 Audition (rating export)"
  .Event OnFileImportMenuClick
    .Let ImportOrigin="FINAN_XML"
    .DoScript GeneralImportScript
  .EndScript
.EndIf

;—————————————————————————————————————————————————————————————————————————————————————————————————————————————
; CaseWare (2.4 and Financials)

.If FileExists("#ApplicationFolder#\FD_GENERIC_XML_SIL.dll")
.Script Name=CasewareImportScript,Title="CaseWare"
.Event OnFileImportMenuClick
  .Let ImportOrigin="GENERIC_XML"
  .DoScript GeneralImportScript
.EndScript

;—————————————————————————————————————————————————————————————————————————————————————————————————————————————
; XML Audit File

.If FileExists("#ApplicationFolder#\FD_AUDIT_XML_SIL.dll")
  .Script Name=AuditFileImportScript,Title="XML Audit File"
  .Event OnFileImportMenuClick
    .Let ImportOrigin="XAF"
    .DoScript GeneralImportScript
  .EndScript
.EndIf

;—————————————————————————————————————————————————————————————————————————————————————————————————————————————
; XBRL

.If FileExists("#ApplicationFolder#\XBRL_SIL.dll")
  .Script Name=XBRLImportScript,Title="XBRL"
  .Event OnFileImportMenuClick
    .Let ImportOrigin="XBRL"
    .Application SmartImportMode=On                             ; EJS 11-6-2013 aangezet! ivm testen Belgische XBRL, werkt goed
    .DoScript GeneralImportScript
    .Application SmartImportMode=Off
  .EndScript
.EndIf

;—————————————————————————————————————————————————————————————————————————————————————————————————————————————
; iMuis

.If FileExists("#ApplicationFolder#\FinanXML_SIL.dll")
  .Script Name=iMuisImportScript,Title="iMuis"
  .Event OnFileImportMenuClick
    .Let ImportOrigin="FINAN_XML"
    .DoScript GeneralImportScript
  .EndScript
.EndIf

;—————————————————————————————————————————————————————————————————————————————————————————————————————————————
; finan.xml

.If FileExists("#ApplicationFolder#\FinanXML_SIL.dll")
  .Script Name=FinanXmlImportScript,Title="FINAN XML"
  .Event OnFileImportMenuClick
    .Let ImportOrigin="FINAN_XML"
    .DoScript GeneralImportScript
  .EndScript
.EndIf

;—————————————————————————————————————————————————————————————————————————————————————————————————————————————
; finan.csv

.If FileExists("#ApplicationFolder#\FinanCSV_SIL.dll")
  .Script Name=FinanCsvImportScript,Title="FINAN CSV"
  .Event OnFileImportMenuClick
    .Let ImportOrigin="FINAN_CSV"
    .DoScript GeneralImportScript
  .EndScript
.EndIf

;—————————————————————————————————————————————————————————————————————————————————————————————————————————————
; FINAN Classic Data File

.Script Name=ImportClassicDataFile,Title="$>FINAN Classic Data File<$",
.Event OnFileImportMenuClick
  .ScriptMode ErrorMode=Simple           ; suppress script name and line number in error messages
  .OpenDocument ClassicDataFile,Model="V04",RelatedDocuments=No,ExternalLinks=No
.EndScript

;—————————————————————————————————————————————————————————————————————————————————————————————————————————————
; King

.If FileExists("#ApplicationFolder#\FD_KING_FINAN_XML_SIL.dll")
  .Script Name=KingImportScript,Title="King boekhoudpakket"
  .Event OnFileImportMenuClick
    .Let ImportOrigin="XML-KING-FINAN"
    .DoScript GeneralImportScript
  .EndScript
.EndIf

;—————————————————————————————————————————————————————————————————————————————————————————————————————————————
; Caseware voor ABAB

.If FileExists("#ApplicationFolder#\FD_ABAB_SIL.dll")
  .Script Name=AbabmportScript,Title="Caseware bij ABAB"
  .Event OnFileImportMenuClick
    .Let ImportOrigin="Caseware ABAB"
    .DoScript GeneralImportScript
  .EndScript
.EndIf

;—————————————————————————————————————————————————————————————————————————————————————————————————————————————
; FINAN Classic Data Files (entire directory)

.Script Name=ImportClassicDataFiles,Title="$>FINAN Classic Data Files<$ ($>Entire directory<$)",Priority=1001
.Event OnFileImportMenuClick
  .FolderDialog &ClassicDataFolder,Folder="#ImportFolder#"
  .FileDialog &LogFileName,Mode=Save,Caption="$>Import data files: append log to file<$",Filter="$>log file<$ (*.txt)|*.txt",FileName="importlog.txt",Folder="#$UserFolder#"
  .Application BeginSilent
  .Let &ImportCounter = 0
  .Let &FileCounter = 0
  .Log DateTime,File="#LogFileName#"
  .Log "$>Start import from folder ""$_#ClassicDataFolder#_$""<$"
  .FindFirstFile FileName="#ClassicDataFolder#\*.*"
  .If ("#$FileName#"<>"")
    .Repeat
      .Let &FileCounter = #FileCounter#+1
      .Let &ScriptError=0
      .OpenDocument ClassicDataFile="#ClassicDataFolder#\#$FileName#",Model="V04",RelatedDocuments=No,ExternalLinks=No    ; hier model toevoegen, omdat modelkeuze dialoog niet kan in SilentMode
      .Case #OpenDocumentResult#
      .OnValue 0 ; no error
        .SaveDocument
        .Log "#FileCounter#: #FileName# $>imported<$"
        .Let &ImportCounter = #ImportCounter#+1
        .CloseDocument
      .OnValue 3 ; create error
        .Log "#FileCounter#: #FileName# $>NOT IMPORTED<$ ($>unable to create target<$)"
      .OnValue 4 ; open error
        .Log "#FileCounter#: #FileName# $>NOT IMPORTED<$ ($>unable to open source<$)"
      .OnValue 5 ; convert error
        .Log "#FileCounter#: #FileName# $>NOT IMPORTED<$ ($>unable to convert<$)"
      .OnValue 6 ; overwrite denied
        .If &AfterStr(".","#FileName#")<>"V04"
          .OpenDocument ClassicDataFile="#ClassicDataFolder#\#$FileName#",Key=&BeforeStr(".","#FileName#")&"-"&AfterStr(".","#FileName#"),Model="V04",RelatedDocuments=No,ExternalLinks=No
          .If #OpenDocumentResult#=0
            .SaveDocument
            .Log "#FileCounter#: $>""$_#FileName#_$"" imported, renamed to ""$_#DataKey#_$""<$"
            .Let &ImportCounter = #ImportCounter#+1
            .CloseDocument
          .Else
            .Log "#FileCounter#: #FileName# $>NOT IMPORTED<$ ($>target exists<$)"
          .EndIf
        .Else
          .Log "#FileCounter#: #FileName# $>NOT IMPORTED<$ ($>target exists<$)"
        .EndIf
      .OnValue 7 ; not a valid data script
        .Log "#FileCounter#: #FileName# $>NOT IMPORTED<$ ($>invalid data<$)"
      .OnValue   ; other errors
        .Log "#FileCounter#: #FileName# $>NOT IMPORTED<$"
      .EndCase
      .FindNextFile
    .Until ("#$FileName#"="")
  .EndIf
  .Log "$>Number of files imported:<$ #$ImportCounter#"
  .Log "$>Total number of files in source directory:<$ #$FileCounter#"
  .Log ""
  .Application EndSilent
.EndScript


;=============================================================================================================
; General import script

; aandachtspunten:
; - gebroken boekjaar (gaat waarschijnlijk niet goed...)
; - SysVar ImportOrigin moet een waarde hebben

.Script Name=GeneralImportScript
;----importeer data als nieuw dossier indien er geen actief dossier is:
  .If MaxDocumentIndex=-1
    .OpenDocument Model="V04",Origin=&SysVar("ImportOrigin"),Overwrite=Off
    .Quit
  .EndIf
;----status instellingen:
  .Application BeginUpdate
  .Let &ImportTargetKey=&SysVar("DataKey")                                      ; EJS 12-08-2013 huidige DataKey (boventabblad)
  .ScriptMode CancelMode=Off,ErrorMode=Simple                                   ; EJS 22-07-2013 ErrorMode=Simple toegevoegd
;----importeer data in nieuw boventabblad:
  .OpenDocument Model="V04",Origin=&SysVar("ImportOrigin"),AsRelatedDocument,Overwrite=Off,NoDio
  .If #OpenDocumentResult#<>0
    .Application EndUpdate
    .Error "Import mislukt!"
    .Quit
  .EndIf
;----kopieer nieuwe kolommen in bestaand dossier (target document):
  .Let &Ok=False
  .Document Key=&SysVar("ImportTargetKey")                                      ; EJS 22-07-2013  was eerst .Document Index=0
  .Let &FirstTargetDate=FirstDateInT(FirstTinPeriod(MainPeriod))
  .Let &LastTargetDate=LastDateInT(LastTinFormulaSet(NoTrend,MainPeriod))
  .Document Index=MaxDocumentIndex                                           ; Source document
  .Calc 1                                                                    ; nodig ivm FirstValueT(BalPas,..) hieronder !
  .Let &ImportSourceKey="#$DataKey#"
  .Let &FirstImportDate=OnNotPos(FirstDateInT(FirstValueT(BalPas,1,LastTinFormulaSet(NoTrend,MainPeriod))),#LastTargetDate#+1) ;# nu alleen kolommen bepalen op basis van balans, ook ResRek?
  .Let &LastImportDate=OnNotPos(LastDateInT(LastValueT(BalPas,1,LastTinFormulaSet(NoTrend,MainPeriod))),#FirstTargetDate#-1)   ;# nu alleen kolommen bepalen op basis van balans, ook ResRek?
  .If (#FirstImportDate#<#LastTargetDate#) And (#LastImportDate#>#FirstTargetDate#)
    ; er zijn import cijfers aanwezig die overlappen met één of meer bestaande (target) kolommen
    .Let Ok=False
    .AskOk Ok="Cijfers van "&DateStr(#FirstImportDate#)&" t/m "&DateStr(#LastImportDate#)&" gevonden in bronbestand.|Bestaande gegevens (overlap is "&DateStr(Max(#FirstImportDate#,#FirstTargetDate#))&" t/m "&DateStr(Min(#LastImportDate#,#LastTargetDate#))&") overschrijven?",#Ok#
    .If Not #Ok#
      .If #LastImportDate#>#LastTargetDate#                             ; import zit rechts (nieuwer)
        .Let &FirstImportDate=#LastTargetDate#+1                        ; niets overschrijven, achteraan toevoegen
      .Else                                                             ; import zit links (ouder)
        .Let &LastImportDate=#FirstTargetDate#-1                        ; vooraan toevoegen
      .EndIf
    .EndIf
  .EndIf
  .If #FirstImportDate#>#LastImportDate#
    ; er is niets te importeren
    .Message "Geen (additionele) gegevens om in te voeren."
  .Else
    ; hier niet checken of er een gat valt
    .If (#FirstImportDate#<#LastImportDate#)
      .CopySubPeriod SourceKey="#$ImportSourceKey#",TargetKey="#$ImportTargetKey#",SourceSubPeriod="MainPeriod_SP1",TargetSubPeriod="MainPeriod_SP1",FirstDate=#FirstImportDate#,LastDate=#LastImportDate#,OnlyEnteredValues,EraseBeforeCopy,AddSourceColumnsToTarget,SourceTsYinTarget,Freqs=[Detail] ; EJS 22-07-2013 ImportTargetKey ipv MainDataKey
      .Message "Gegevens ("&DateStr(#FirstImportDate#)&" t/m "&DateStr(#LastImportDate#)&") zijn geïmporteerd!"
    .EndIf
  .EndIf
;----verwijder het tijdelijke boventabblad:
  .If MaxDocumentIndex>0
    .Document Index=MaxDocumentIndex
    .CloseDocument ActiveDocument,NoDio
  .EndIf
  .Document Key=&SysVar("ImportTargetKey"),Select                               ; EJS 23-07-2013  toegevoegd
  .SaveDocument
  .Application EndUpdate
.EndScript



;—————————————————————————————————————————————————————————————————————————————————————————————————————————————
; Naverwerking: Bepaal rechtsvorm
;

.Script
.Event AfterImportConversion
  .If Not DataEntered(RechtsVorm,1)
    .If HSum(Kapitaal,FirstTinFormulaSet(NoTrend,MainPeriod),LastTinFormulaSet(NoTrend,MainPeriod))<>NA
      .InputVar Rechtsvorm[1]= 1                             ; BV
    .Else
      .InputVar Rechtsvorm[1]= 11                            ; VOF
    .EndIf
  .EndIf
  .DoScript UpdateLegalStatus                                ; Rechtsvorm script runnen!
.EndScript


;—————————————————————————————————————————————————————————————————————————————————————————————————————————————
; Naverwerking: Overige mutaties eigen vermogen accoord
;

.Script
.Event AfterImportConversionAtT
  .InputVar MaxOvMutEv[T]=1                                  ; Vermogensaansluiting: verschillen akkoord
.EndScript


;=============================================================================================================
; Dit script converteert een V03 document naar V04.
; let op: alleen zinvol indien V03.fin wordt meegeleverd! Dat is inmiddels niet meer het geval!
;
.Script
.Event AfterOpenDocument
  .If (DocumentIndex=0) And ("#ModelCode#"="V03")
    .AskOk ConvertToV04 = "Wilt u de gegevens naar het vernieuwde model (v04) converteren?"
    .If #ConvertToV04#
      .Let &OldDocumentKey = "#FullDataKey#"
      .Document Modified=Off
      .CloseDocument
      .OpenDocument Key="#OldDocumentKey#",Model="V04"
      .If #OpenDocumentResult#=0
        .MoveFile "#OldDocumentKey#" "#OldDocumentKey#.BAK"
        .If #ScriptError# = 2       ; Er is iets foutgegaan bij de MoveFile
          .Let ConversieInfo = "Het oude dossier is niet gewijzigd!"
        .Else
          .Let ConversieInfo = "Het oude dossier is opgeslagen onder de naam '#OldDocumentKey#.BAK'"
        .EndIf
        .Message "Het dossier '#OldDocumentKey#' is geconverteerd naar het vernieuwde V04 model.|#ConversieInfo#|Zie de helpfile voor informatie over de veranderingen."
      .Else
        .Message "Het converteren van dossier '#OldDocumentKey#' is mislukt.|Errorcode: #OpenDocumentResult#"
      .EndIf
    .EndIf
  .EndIf
.EndScript

